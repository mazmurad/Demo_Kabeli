   
#Область ОбработчикиСобытийФормы
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда		
		МодульУправленияДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправленияДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;	
	КонецЕсли;
	
	ОбновлениеИнформационнойБазы.ПроверитьОбъектОбработан(Объект, ЭтотОбъект);
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если Параметры.Свойство("ЖурналУчетаКабельнойПродукции") Тогда
			Объект.ЖурналУчетаКабельнойПродукции = Параметры.ЖурналУчетаКабельнойПродукции;	
		КонецЕсли;  
		
		Если Параметры.Свойство("Подрядчик") Тогда
			Объект.Подрядчик = Параметры.Подрядчик;	
		КонецЕсли;  
		
		Объект.Статус = Перечисления.СтатусыЗаявкиНаЗакупку.Создана;   
		
		Если Параметры.Свойство("ЭтоРабочееМесто") Тогда   
			
			Объект.Дата = ТекущаяДата();
			Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));	
		КонецЕсли;
	КонецЕсли;
	
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	
	ЗапретРедактированияРеквизитовОбъектов.ЗаблокироватьРеквизиты(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;	
	КонецЕсли;
	
	ОповеститьОВыборе(Объект.Ссылка);
	
КонецПроцедуры
#КонецОбласти

#Область ОбработчикиКомандыФормы  
&НаКлиенте
Процедура ПрикрепитьСканДокумента(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ПослеВыбораНомерВК", ЭтотОбъект);	
	ОткрытьФорму("Документ.ЗаявкиНаЗакупки.Форма.ВводВКНаКПП",,ЭтаФорма,
		УникальныйИдентификатор,,,Обработчик,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
				
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)

	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
	
КонецПроцедуры  

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)

	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_ВыполнитьПереопределяемуюКоманду(Команда)

	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтаФорма, Команда);
	
КонецПроцедуры 

&НаКлиенте
Процедура Подключаемый_РазрешитьРедактированиеРеквизитовОбъекта(Команда)

	МодульЗапретРедактированияРеквизитовОбъектовКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ЗапретРедактированияРеквизитовОбъектовКлиент");	
	Оповещение = Новый ОписаниеОповещения("РазрешитьРедактированиеРеквизитовОбъектаЗавершение", ЭтотОбъект);    
	МодульЗапретРедактированияРеквизитовОбъектовКлиент.РазрешитьРедактированиеРеквизитовОбъекта(ЭтотОбъект, Оповещение);
	
КонецПроцедуры 

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовОбъектаЗавершение(Результат, ДополнительныеПараметры) Экспорт

	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаявку(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;	
	КонецЕсли;
	
	Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявок.Выполнена");	
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	
КонецПроцедуры      

&НаКлиенте
Процедура ОтменитьВыполнение(Команда)

	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;	
	КонецЕсли;
	
	Объект.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявок.Создана");	
	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаписатьДокумент(Команда)

	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));

КонецПроцедуры
#КонецОбласти   

#Область СобытияПолейФормы
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если ЭтоИзменениеТекущейЗаявки() Тогда
		Записать();	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура СтатусПриИзменении(Элемент)

	Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Проведение));
	
КонецПроцедуры

&НаКлиенте
Процедура НомерВКнаКПППриИзменении(Элемент)

	Если ЭтоИзменениеТекущейЗаявки() Тогда
		Записать();	
	КонецЕсли;	

КонецПроцедуры 

&НаКлиенте
Функция ЭтоИзменениеТекущейЗаявки()
	
	Возврат ЗначениеЗаполнено(Объект.Ссылка);
	
КонецФункции
#КонецОбласти   

#Область ПрочиеПроцедурыИФункции
&НаКлиенте
Процедура ПослеВыбораНомерВК(Результат, ДопПараметры) Экспорт   
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда 
		Возврат;
	КонецЕсли;	
	
	НомерВК = Результат;
	
	ОбработчикВыбораФайла = Новый ОписаниеОповещения("ПродолжитьВыборСканДокумента", ЭтотОбъект, НомерВК);
	РаботаСФайламиКлиент.ДобавитьФайлы(Объект.Ссылка, УникальныйИдентификатор,,Неопределено, ОбработчикВыбораФайла);	
	
КонецПроцедуры	    

&НаКлиенте
Процедура ПродолжитьВыборСканДокумента(Результат, НомерВК) Экспорт  
		
	Для Каждого ТекДанные Из Объект.КабельныеЛинии Цикл  
		ТекДанные.НомерВКНаКПП = НомерВК;
	КонецЦикла;
	
	Записать();
	
КонецПроцедуры	
#КонецОбласти

