  
#Область ОбработчикиСобытийФормы 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("Отбор") Тогда		
		ТекстУсловия = ПолучитьТекстОтбораСписка(Параметры.Отбор);
		УстановитьУсловиеВТекстЗапросаСписка(ТекстУсловия);
	КонецЕсли;	

КонецПроцедуры
#КонецОбласти
  
#Область ОбработчикиСобытийТаблицыФормыСписокКабельныеЛинии
&НаКлиенте
Процедура СписокКабельныеЛинииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;	
	
	ОтборВыбранные = Новый Структура("КабельнаяЛиния", ВыбраннаяСтрока);
	РанееВыбранные = ВыбранныеКабельныеЛинии.НайтиСтроки(ОтборВыбранные);
	Если РанееВыбранные.Количество() <> 0 Тогда
		Возврат;	
	КонецЕсли;  
	
	НовВыбранная                = ВыбранныеКабельныеЛинии.Добавить();
	НовВыбранная.КабельнаяЛиния = ВыбраннаяСтрока;      
	НовВыбранная.ЖурналУчетаКЛ  = Элемент.ТекущиеДанные.ЖурналУчетаКЛ;
	СписокВыбранныхКабельныхЛиний.Добавить(ВыбраннаяСтрока, ВыбраннаяСтрока);
	
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиСобытийТаблицыФормыВыбранныеКабельныеЛинии
&НаКлиенте
Процедура ВыбранныеКабельныеЛинииПередУдалением(Элемент, Отказ)
	
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	КабельнаяЛиния = Элемент.ТекущиеДанные.КабельнаяЛиния;	
	УдаляемаяЛиния = СписокВыбранныхКабельныхЛиний.НайтиПоЗначению(КабельнаяЛиния);
	СписокВыбранныхКабельныхЛиний.Удалить(УдаляемаяЛиния);
	
КонецПроцедуры
#КонецОбласти


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ЗавершитьВыбор(Команда)
	
	ВыбранныеДанные = Новый Массив;
	Для Каждого СтрокаВыбранные Из ВыбранныеКабельныеЛинии Цикл
		СтруктураВыбранныхДанных = Новый Структура;
		СтруктураВыбранныхДанных.Вставить("КабельнаяЛиния", СтрокаВыбранные.КабельнаяЛиния);
		СтруктураВыбранныхДанных.Вставить("ЖурналУчетаКП",  СтрокаВыбранные.ЖурналУчетаКЛ);		
		ВыбранныеДанные.Добавить(СтруктураВыбранныхДанных);
	КонецЦикла;	
	
	Если ВыбранныеДанные.Количество() > 0 Тогда
		Закрыть(ВыбранныеДанные);
	Иначе
		Закрыть();
	КонецЕсли;	
	
КонецПроцедуры
#КонецОбласти


#Область СлужебныеПроцедурыФункции 
&НаСервере
Функция ПолучитьТекстОтбораСписка(НазваниеОтбора) 
	
	Если НазваниеОтбора = "ПоРегиструЗаданияЗакупкиПринятыеКабельныеЛинии" Тогда
		Возврат "СправочникКабельныеЛинии.Ссылка В (Выбрать РС.КабельняЛиния Из РегистрСведений.ЗаданияНаЗакупкиПринятыеКабельныеЛинии Как РС)";   
	ИначеЕсли НазваниеОтбора = "ОтборВКПройден" Тогда
		Возврат "СправочникКабельныеЛинии.Ссылка В (Выбрать РС.КабельнаяЛиния Из РегистрСведений.АктВходногоКонтроляНаКЛ Как РС Где РС.Состояние = Значение(Перечисление.СтатусыВК.ВКПройден))";
	КонецЕсли;	
	Возврат "Истина";
	
КонецФункции	

&НаСервере
Процедура УстановитьУсловиеВТекстЗапросаСписка(ТекстУсловия)   

	ТекстЗапроса = СписокКабельныеЛинии.ТекстЗапроса;
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "1 = 1", ТекстУсловия);
	СписокКабельныеЛинии.ТекстЗапроса = ТекстЗапроса;	

КонецПроцедуры	
#КонецОбласти