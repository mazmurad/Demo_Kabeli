
Функция ПрочитатьПросроченныеЗаявкиНаМонтаж() Экспорт   
	
	СрокВыполненияЗаявок = Константы.СрокВыполненияЗаявок.Получить();
	Если СрокВыполненияЗаявок = 0 Тогда 
		Возврат Неопределено;
	КонецЕсли;	
	
	Запрос       = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЗаявкаНаМонтаж.Дата КАК ДатаСозданияЗаявки,
		|	ЗаявкаНаМонтаж.Ссылка КАК ЗаявкаНаМонтаж,
		|	РАЗНОСТЬДАТ(ЗаявкаНаМонтаж.Дата, &ТекущаяДата, СЕКУНДА) КАК ПрошлоСекунд
		|ПОМЕСТИТЬ ВТ_ЗаявкиНаМонтажНаРассмотрение
		|ИЗ
		|	Документ.ЗаявкаНаМонтаж КАК ЗаявкаНаМонтаж
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РазрешениеМонтажаКПП КАК РазрешениеМонтажаКПП
		|		ПО ЗаявкаНаМонтаж.Ссылка = РазрешениеМонтажаКПП.Регистратор
		|ГДЕ
		|	НЕ ЗаявкаНаМонтаж.ПометкаУдаления
		|	И РазрешениеМонтажаКПП.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусаМонтажаКППНаОбъекте.ЗапросНаРасмотрении)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_ЗаявкиНаМонтажНаРассмотрение.ЗаявкаНаМонтаж КАК ЗаявкаНаМонтаж
		|ИЗ
		|	ВТ_ЗаявкиНаМонтажНаРассмотрение КАК ВТ_ЗаявкиНаМонтажНаРассмотрение
		|ГДЕ
		|	ВТ_ЗаявкиНаМонтажНаРассмотрение.ПрошлоСекунд >= &КоличествоСекундДоПросрочки";
	
	ТекущаяДата = ТекущаяДатаСеанса();
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДата);
	Запрос.УстановитьПараметр("КоличествоСекундДоПросрочки", СрокВыполненияЗаявок);
	
	РезультатЗапроса      = Запрос.Выполнить();	
	ТаблицаЗаявокНаМонтаж = РезультатЗапроса.Выгрузить();
	СписокЗаявокНаМонтаж  = ТаблицаЗаявокНаМонтаж.ВыгрузитьКолонку("ЗаявкаНаМонтаж");
	
    Возврат СписокЗаявокНаМонтаж;
	
КонецФункции

Функция ОповеститьТекущегоПользователя() Экспорт     
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();

	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	1 КАК Оповещаем
	               |ИЗ
	               |	РегистрСведений.ПереченьПользователейДляОповещения КАК ПереченьПользователейДляОповещения
	               |ГДЕ
	               |	ПереченьПользователейДляОповещения.Пользователь = &ТекущийПользователь";
	Запрос.УстановитьПараметр("ТекущийПользователь", ТекущийПользователь);
	Выборка = Запрос.Выполнить().Выбрать();

	Возврат ?(Выборка.Следующий(), Истина, Ложь);
	
КонецФункции    

