
Процедура ВыборПодрядчикаИКабельныеЛинии(ПараметрыОбъекта) Экспорт
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("Форма",                ПараметрыОбъекта.Форма); 
	ДопПараметры.Вставить("ИмяОбъекта",           ПараметрыОбъекта.ИмяОбъекта); 
	ДопПараметры.Вставить("ОтборыКабельныхЛиний", ?(ПараметрыОбъекта.Свойство("ОтборыКабельныхЛиний"), 
		ПараметрыОбъекта.ОтборыКабельныхЛиний, Неопределено));

	Если ЗначениеЗаполнено(ПараметрыОбъекта.Подрядчик) Тогда    
		ПродолжитьВыполнениеПослеВыбораПодрядчика(ПараметрыОбъекта.Подрядчик, ДопПараметры);	
	Иначе	
		ОбработкаВыбораПодрядчика = Новый ОписаниеОповещения("ПродолжитьВыполнениеПослеВыбораПодрядчика", ЭтотОбъект, ДопПараметры);    
		ПараметрыОбъекта.СписокВыбораПодрядчика.ПоказатьВыборЭлемента(ОбработкаВыбораПодрядчика, "Выбор подрядчика");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьВыполнениеПослеВыбораПодрядчика(РезультатВыбора, ДопПараметры) Экспорт
	
    Если РезультатВыбора = Неопределено Тогда
        ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Подрядчик не выбран"); 
		Возврат;
	КонецЕсли;
	Подрядчик = ?(ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.Подрядчик"), РезультатВыбора, РезультатВыбора.Значение);
	
	ДопПараметрыДальше = Новый Структура;
	ДопПараметрыДальше.Вставить("Подрядчик",  Подрядчик); 
	ДопПараметрыДальше.Вставить("ИмяОбъекта", ДопПараметры.ИмяОбъекта);
	ДопПараметрыДальше.Вставить("Форма",      ДопПараметры.Форма);
	
	ОтборыКабельныхЛиний = Новый Структура;
	ОтборыКабельныхЛиний.Вставить("Отбор", ДопПараметры.ОтборыКабельныхЛиний);
	
	ОбработкаПослеВыбораКабельныхЛиний = Новый ОписаниеОповещения("ПродолжитьВыполнениеПослеВыбораКабельныхЛиний", ЭтотОбъект, ДопПараметрыДальше);
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораКабельныхЛиний", ОтборыКабельныхЛиний, ДопПараметры.Форма, ДопПараметры.Форма.УникальныйИдентификатор,,, ОбработкаПослеВыбораКабельныхЛиний,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры
	
Процедура ПродолжитьВыполнениеПослеВыбораКабельныхЛиний(РезультатВыбора, ДопПараметры) Экспорт
	
    Если РезультатВыбора = Неопределено Тогда
        ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Кабельные линии не выбраны"); 
		Возврат;
	КонецЕсли;
	
	Подрядчик = ДопПараметры.Подрядчик; 
	Если ДопПараметры.ИмяОбъекта = "ЗадниеНаЗакупку" Тогда 
		КлючОбъекта = ДопПараметры.Форма.СоздатьЗаданиеНаЗакупки(Подрядчик, РезультатВыбора);	 
		ИмяФормы    = "Документ.ЗаданияНаЗакупку.ФормаОбъекта";   
	ИначеЕсли ДопПараметры.ИмяОбъекта = "ЗаявкиНаЗакупку" Тогда
		КлючОбъекта = ДопПараметры.Форма.СоздатьЗаявкуНаЗакупки(Подрядчик, РезультатВыбора);	 
		ИмяФормы    = "Документ.ЗаявкиНаЗакупки.ФормаОбъекта";  
	ИначеЕсли ДопПараметры.ИмяОбъекта = "ЗаявкаНаМонтаж" Тогда	
		КлючОбъекта = ДопПараметры.Форма.СоздатьЗаявкуНаМонтаж(Подрядчик, РезультатВыбора);	 
		ИмяФормы    = "Документ.ЗаявкаНаМонтаж.ФормаОбъекта";  		
	КонецЕсли;	
	
	ОбновитьИнтерфейс();
	
	ПараметрыОткрытия = Новый Структура("Ключ", КлючОбъекта);
	ОткрытьФорму(ИмяФормы, ПараметрыОткрытия, ДопПараметры.Форма)

КонецПроцедуры 	