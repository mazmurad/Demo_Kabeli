
Процедура ПроверитьЕстьСвязанныеДокументыПередПометкойУдаления(Ссылка, СписокСвязанныхДокументов, Отказ) Экспорт

	Если СписокСвязанныхДокументов.Количество() > 0 Тогда 
		
		Отказ = Истина;
		ТекстСообщения = СтрШаблон("Нельзя удалить текущий документ %1, есть связанные документы: ", Ссылка);
		Для Каждого ТекДокумент Из СписокСвязанныхДокументов Цикл 
			ТекстСообщения = ТекстСообщения + Символы.ПС + ТекДокумент; 
		КонецЦикла;	 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьСвязанныеДокументыЗаданияНаЗакупки(СписокЗаданийНаЗакупки) Экспорт
	
	УстановитьПривилегированныйРежим(ИСтина);	
	Запрос       = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаданияНаЗакупку.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ ВТ_УдаляемыеЗаданияНаЗакупки
	               |ИЗ
	               |	Документ.ЗаданияНаЗакупку КАК ЗаданияНаЗакупку
	               |ГДЕ
	               |	ЗаданияНаЗакупку.Ссылка В(&СписокЗаданийНаЗакупки)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаданияНаЗакупкуКабельныеЛинии.ЖурналУчетаКП КАК ЖурналУчетаКП,
	               |	ЗаданияНаЗакупкуКабельныеЛинии.КабельнаяЛиния КАК КабельнаяЛиния
	               |ПОМЕСТИТЬ ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки
	               |ИЗ
	               |	ВТ_УдаляемыеЗаданияНаЗакупки КАК ВТ_УдаляемыеЗаданияНаЗакупки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаданияНаЗакупку.КабельныеЛинии КАК ЗаданияНаЗакупкуКабельныеЛинии
	               |		ПО ВТ_УдаляемыеЗаданияНаЗакупки.Ссылка = ЗаданияНаЗакупкуКабельныеЛинии.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаявкиНаЗакупкиКабельныеЛинии.Ссылка КАК ЗаявкаНаЗакупки
	               |ПОМЕСТИТЬ ВТ_СвязанныеЗаявкиНаЗакупки
	               |ИЗ
	               |	Документ.ЗаявкиНаЗакупки.КабельныеЛинии КАК ЗаявкиНаЗакупкиКабельныеЛинии
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки КАК ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки
	               |		ПО (ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки.ЖурналУчетаКП = ЗаявкиНаЗакупкиКабельныеЛинии.ЖурналУчетаКП)
	               |			И ЗаявкиНаЗакупкиКабельныеЛинии.КабельнаяЛиния = ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки.КабельнаяЛиния
	               |			И (НЕ ЗаявкиНаЗакупкиКабельныеЛинии.Ссылка.ПометкаУдаления)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ЗаявкаНаМонтажКабельныеЛинии.Ссылка КАК ЗаявкаНаМонтаж
	               |ПОМЕСТИТЬ ВТ_ЗаявкиНаМонтаж
	               |ИЗ
	               |	ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки КАК ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаМонтаж.КабельныеЛинии КАК ЗаявкаНаМонтажКабельныеЛинии
	               |		ПО (НЕ ЗаявкаНаМонтажКабельныеЛинии.Ссылка.ПометкаУдаления)
	               |			И ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки.ЖурналУчетаКП = ЗаявкаНаМонтажКабельныеЛинии.ЖурналУчетаКП
	               |			И ВТ_КабельныеЛинииУдаляемыхЗаданийНаЗакупки.КабельнаяЛиния = ЗаявкаНаМонтажКабельныеЛинии.КабельнаяЛиния
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_СвязанныеЗаявкиНаЗакупки.ЗаявкаНаЗакупки КАК СвязанныйДокумент
	               |ИЗ
	               |	ВТ_СвязанныеЗаявкиНаЗакупки КАК ВТ_СвязанныеЗаявкиНаЗакупки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ВТ_ЗаявкиНаМонтаж.ЗаявкаНаМонтаж
	               |ИЗ
	               |	ВТ_ЗаявкиНаМонтаж КАК ВТ_ЗаявкиНаМонтаж";
	
	Запрос.УстановитьПараметр("СписокЗаданийНаЗакупки", СписокЗаданийНаЗакупки);
	Результат = Запрос.Выполнить().Выгрузить();
	СписокСвязанныхДокументов = Результат.ВыгрузитьКолонку("СвязанныйДокумент");
	
	Возврат СписокСвязанныхДокументов;
	
КонецФункции

Функция ПолучитьСвязанныеДокументыЗаявкиНаЗакупки(СписокЗаявокНаЗакупки) Экспорт
	
	УстановитьПривилегированныйРежим(ИСтина);	
	Запрос       = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ЗаявкиНаЗакупки.Ссылка КАК Ссылка
	               |ПОМЕСТИТЬ ВТ_УдаляемыеЗаданияНаЗакупки
	               |ИЗ
	               |	Документ.ЗаявкиНаЗакупки КАК ЗаявкиНаЗакупки
	               |ГДЕ
	               |	ЗаявкиНаЗакупки.Ссылка В(&СписокЗаявокНаЗакупки)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаявкиНаЗакупкиКабельныеЛинии.ЖурналУчетаКП КАК ЖурналУчетаКП,
	               |	ЗаявкиНаЗакупкиКабельныеЛинии.КабельнаяЛиния КАК КабельнаяЛиния
	               |ПОМЕСТИТЬ ВТ_КабельныеЛинииУдаляемыхЗаявкиНаЗакупки
	               |ИЗ
	               |	ВТ_УдаляемыеЗаданияНаЗакупки КАК ВТ_УдаляемыеЗаданияНаЗакупки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкиНаЗакупки.КабельныеЛинии КАК ЗаявкиНаЗакупкиКабельныеЛинии
	               |		ПО ВТ_УдаляемыеЗаданияНаЗакупки.Ссылка = ЗаявкиНаЗакупкиКабельныеЛинии.Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |	ЗаявкаНаМонтажКабельныеЛинии.Ссылка КАК ЗаявкаНаМонтаж
	               |ПОМЕСТИТЬ ВТ_ЗаявкиНаМонтаж
	               |ИЗ
	               |	ВТ_КабельныеЛинииУдаляемыхЗаявкиНаЗакупки КАК ВТ_КабельныеЛинииУдаляемыхЗаявкиНаЗакупки
	               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗаявкаНаМонтаж.КабельныеЛинии КАК ЗаявкаНаМонтажКабельныеЛинии
	               |		ПО (НЕ ЗаявкаНаМонтажКабельныеЛинии.Ссылка.ПометкаУдаления)
	               |			И ВТ_КабельныеЛинииУдаляемыхЗаявкиНаЗакупки.ЖурналУчетаКП = ЗаявкаНаМонтажКабельныеЛинии.ЖурналУчетаКП
	               |			И ВТ_КабельныеЛинииУдаляемыхЗаявкиНаЗакупки.КабельнаяЛиния = ЗаявкаНаМонтажКабельныеЛинии.КабельнаяЛиния
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_ЗаявкиНаМонтаж.ЗаявкаНаМонтаж КАК СвязанныйДокумент
	               |ИЗ
	               |	ВТ_ЗаявкиНаМонтаж КАК ВТ_ЗаявкиНаМонтаж";
	
	Запрос.УстановитьПараметр("СписокЗаявокНаЗакупки", СписокЗаявокНаЗакупки);
	Результат = Запрос.Выполнить().Выгрузить();
	СписокСвязанныхДокументов = Результат.ВыгрузитьКолонку("СвязанныйДокумент");
	
	Возврат СписокСвязанныхДокументов;
	
КонецФункции	          

