
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
			
	Если Не ЗначениеЗаполнено(Ссылка) Тогда
		
		Если Не РазрешенМонтаж() Тогда
			ОбщегоНазначения.СообщитьПользователю("Нет разрешения на монтаж по журналу: " + ЖурналУчетаКабельнойПродукции,,,, Отказ);	
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоОтменаМонтажа() И СтатусыПроверкаСервер.ЖурналКабельнойЛинииПодписан(ЖурналУчетаКабельнойПродукции) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Работа сдана, нельзя отменить документ!",,,, Отказ);	
	КонецЕсли;	
	
КонецПроцедуры  

Функция РазрешенМонтаж()
	
	Возврат ЖурналУчетаКабельнойПродукции.РазрешитьМонтаж;	
	
КонецФункции

Функция ЭтоОтменаМонтажа()
	
	ТекущийСтатусВыполнен = (Ссылка.Статус = Перечисления.СтатусыМонтажа.Выполнен);
	СменаСтатуса          = (Статус <> Ссылка.Статус);
	
	Возврат ТекущийСтатусВыполнен И СменаСтатуса;
	
КонецФункции

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.ДанныеПоМонтажуКабельнойПродукции.Записывать = Истина;
	
	НоваяЗапись = Движения.ДанныеПоМонтажуКабельнойПродукции.Добавить();
	НоваяЗапись.Период                    = Дата;
	НоваяЗапись.ЖурналУчетаКабельнойЛинии = ЖурналУчетаКабельнойПродукции;    
	НоваяЗапись.Подрядчик                 = Подрядчик;
	НоваяЗапись.Статус                    = Статус; 
	НоваяЗапись.ДлинаФактическая          = ДлинаФактическая; 
	НоваяЗапись.Исполнитель               = Исполнитель;
	
КонецПроцедуры





